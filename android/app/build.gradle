import groovy.json.JsonSlurper

apply plugin: 'com.android.application'

def signConfigFile = new File("./signConfig.json")
def signConfigJson = new JsonSlurper().parseText(signConfigFile.text)

def (versionMajor, versionMinor, versionPatch) = newVersion.tokenize( '.' )
def newVersionCode = versionMajor.toInteger() * 10000 + versionMinor.toInteger() * 100 + versionPatch.toInteger()
println "newVersion: " + newVersion
println "newVersionCode: " + newVersionCode

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.publicradionative"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode newVersionCode
        versionName newVersion
        ndk {
            abiFilters "armeabi-v7a", "x86"
        }
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile file("public-radio.jks")
            storePassword signConfigJson.storePassword
            keyAlias signConfigJson.keyAlias
            keyPassword signConfigJson.keyPassword
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false  // Set this to true to enable Proguard
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

apply plugin: 'com.github.triplet.play'

play {
    jsonFile = file('keys.json')
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:support-v4:23.0.0'
    compile 'com.android.support:appcompat-v7:23.0.0'
    compile 'com.android.support:cardview-v7:23.0.0'
    compile 'com.facebook.react:react-native:0.13.+'

    compile project(':vksdk_library')

    compile project(':react-native-vector-icons')
}
